apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdk 34
    defaultConfig {
        applicationId "ru.ssau.arwalls.bread"

        // AR Optional apps must declare minSdkVersion >= 14.
        // AR Required apps must declare minSdkVersion >= 24.
        minSdkVersion 24
        targetSdkVersion 33
        def majorVersionCode = 0
        def minorVersionCode = 1
        versionCode majorVersionCode * 100000 + minorVersionCode * 1000 + generateVersionCode()
        versionName "$majorVersionCode.$minorVersionCode.${generateVersionCode()}"
        setProperty("archivesBaseName", "Arwalls-bread" + "-v" + versionName)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            storePassword properties.getProperty('storePassword')
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    buildFeatures {
        viewBinding = true
    }
    namespace 'ru.ssau.arwalls.rawdepth'
}

dependencies {
    // ARCore library
    implementation 'com.google.ar:core:1.42.0'

    // Obj - a simple Wavefront OBJ file loader
    // https://github.com/javagl/Obj
    implementation 'de.javagl:obj:0.2.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.fragment:fragment-ktx:1.7.0'
}

static def generateVersionCode() {
    return "git rev-list HEAD --count".execute().text.trim().toInteger()
}
